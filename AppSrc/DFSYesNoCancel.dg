//****************************************************************************
// $Module type: Modal Dialog
// $Module name: YesNoCancel.dg
// $Author     : Nils Svedmyr, RDC Tools International, <mailto:support@rdctools.com>
// Web-site    : http://www.rdctools.com
// Created     : 2020-01-02 @ 09:33 (Military date format: YY-MM-DD)
//
// Purpose     : To have a YesNoCancel dialog with a more user interface,
//               than the standard DF box.
// Icons       : DFSWarning.ico, DFSSave.ico, DFSTrash.ico, DFSUndo.ico

Use Windows.pkg
Use cCommandLinkButton.pkg
// Language constant declarations
// They have all been placed in this central spot to make translations to various languages more convenient.
Use cDataFolderLanguageConstants.inc

Object oYesNoCancel_dg is a ModalPanel
    Set Size to 134 178
    Set Label to "Unsaved Changes"
    Set Label to CS_DFSUnsavedChanges
    Set Color to clWhite
    Set Border_Style to Border_Normal
    Set Locate_Mode to Center_On_Parent 
    
    Property String psCaption
    Property String psInfoText
    Property Integer piRetval MBR_Cancel
    Property Integer piDefaultBtn MB_DEFBUTTON3
        
    Object oInfo_tb is a TextBox
        Set Auto_Size_State to False
        Set Size to 21 151
        Set Location to 8 21
        Set Label to "The file has unsaved changes. What would you like to do?" 
        Set Label to CS_DFSUnsavedChangesInfo
        Set Color to clWhite
        Set Justification_Mode to JMode_Left
    End_Object

    Object oSave_btn is a cCommandLinkButton
        Set Size to 28 170
        Set Location to 33 4
        Set Label to "Save"
        Set Label to CS_DFSSaveText
        Set psImage to "DFSSave.ico"
        Set psNote to "Save the file, and then close the program."
        Set psNote to CS_DFSSaveNote
        Procedure OnClick
            Set piRetval to MBR_Yes
            Send Close_Panel
        End_Procedure
    End_Object
    
    Object oDontSave_btn is a cCommandLinkButton
        Set Size to 28 170
        Set Location to 64 4
        Set Label to "Do&n't Save"
        Set Label to CS_DFSDontSaveText
        Set psImage to "DFSTrash.ico"
        Set psNote to "Discard unsaved changes."
        Set psNote to CS_DFSDontSaveNote
        Procedure OnClick
            Set piRetval to MBR_No
            Send Close_Panel
        End_Procedure
    End_Object

    Object oCancel_btn is a cCommandLinkButton
        Set Size to 28 170
        Set Location to 95 4
        Set Label to "Cancel"
        Set Label to CS_DFSCancelText
        Set psImage to "DFSUndo.ico"
        Set psNote to "Go back to the program."
        Set psNote to CS_DFSCancelNote    
        Procedure OnClick  
            Set piRetval to MBR_Cancel
            Send Close_Panel
        End_Procedure
    End_Object

    Procedure Popup
        String sCaption sText
        
        Get psCaption to sCaption
        Set Label to sCaption
        Get psInfoText to sText
        Set Label of oInfo_tb to sText
        Set piRetval to MBR_Cancel
        Forward Send Popup
    End_Procedure
    
    Procedure Page Integer iPageObject   
        Integer iDfltBtn            
        Handle hoBtn
        Set Icon to "DFSWarning.ico"
        Forward Send Page iPageObject
        Get piDefaultBtn to iDfltBtn
        If (iDfltBtn = MB_DEFBUTTON1) Begin
            Move (oSave_btn(Self)) to hoBtn
        End
        If (iDfltBtn = MB_DEFBUTTON2) Begin
            Move (oDontSave_btn(Self)) to hoBtn
        End
        If (iDfltBtn = MB_DEFBUTTON3) Begin
            Move (oCancel_btn(Self)) to hoBtn
        End                         
        Send Activate of hoBtn
    End_Procedure                    

    On_Key Key_Ctrl+Key_S Send KeyAction of oSave_btn
    On_Key Key_Alt+Key_S  Send KeyAction of oSave_btn
    On_Key Key_Ctrl+Key_N Send KeyAction of oDontSave_btn
    On_Key Key_Alt+Key_N  Send KeyAction of oDontSave_btn
    On_Key kCancel        Send KeyAction of oCancel_btn
    On_Key kDownArrow     Send Next
    On_Key kUpArrow       Send Previous
End_Object

// Puclic access method to the dialog above.
// Acts the same as the standard YesNoCancel function.
Function DFSYesNoCancel_Box String sMessage String sTitle Integer iDfltBtn Returns Integer
    Integer iRetval iDflt
    Handle ho
    String sCaption                                                                          
                                                                          
    Move (oYesNoCancel_dg(Self)) to ho
    If (Num_Arguments = 1 or sTitle = '') Begin
        Move CS_DFSUnsavedChanges to sCaption
    End
    Else Begin
        Move sTitle to sCaption
    End
    If (Num_Arguments < 3 or iDfltBtn = 0 or iDfltBtn = MB_DEFBUTTON1) Begin
        Move MB_DEFBUTTON1 to iDflt
    End
    Else If (iDfltBtn = 1 or iDfltBtn = MB_DEFBUTTON2) Begin
        Move MB_DEFBUTTON2 to iDflt
    End
    Else Begin
        Move MB_DEFBUTTON3 to iDflt
    End
    
    Set psCaption    of ho to sCaption
    Set psInfoText   of ho to sMessage
    Set piDefaultBtn of ho to iDflt
    
    Send Popup of ho
    
    Get piRetval of ho to iRetval
    Function_Return iRetval
End_Function
