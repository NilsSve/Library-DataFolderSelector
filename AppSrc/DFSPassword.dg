//****************************************************************************
// $Module type: Dialog
// $Module name: DFSPassword.dg
// $Author     : Nils Svedmyr, RDC Tools International, <mailto:support@rdctools.com>
// Web-site    : http://www.rdctools.com
// Created     : 2019-11-02 @ 09:33 (Military date format: YY-MM-DD)
//
// Purpose     : To provide rudimentory password protection to the "Data Folder Selector" project.
//               Please see the general purpose access message: PopupDFSPasswordDialog to access
//               the dialog, at the bottom of this code.
//
// See Also    : Check out the SetupDataFolders.chm help file in the Help folder,
//               or press F1 in the SetupDataFolders program.

Use Windows.pkg
Use Trckbr.pkg
Use cDataFolderSelector.pkg

Object oDFSPassword_dg is a ModalPanel
    Set Location to 1 0
    Set Size to 79 209
    Set piMinSize to 75 184
    Set Label to "The Data Folder is Password Protected"
    Set Label to CS_DFSPasswordProtect2
    Set Border_Style to Border_Normal
    Set Locate_Mode to CENTER_ON_PARENT

    Property String psPassword ""
    Property Boolean pbOK False

    Object oInfo_tb is a TextBox
        Set Auto_Size_State to False
        Set Size to 24 157
        Set Location to 8 39
        Set Label to "Please enter a password to protect the program when it starts." 
        Set Label to CS_DFSPasswordInfoText2
        Set Justification_Mode to JMode_Left
    End_Object

    Object oPwdForm is a Form
        Set Label to "Password"
        Set Label to CS_DFSPasswordText
        Set Size to 12 75
        Set Location to 38 72
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        Set Password_State to True
		Set peAnchors to anTopLeftRight

        Procedure OnChange
            String sValue
            Get Value to sValue
            Set psPassword to sValue
        End_Procedure

    End_Object

    Object oOK_btn is a Button
        Set Label to C_$OK
        Set Location to 58 97
        Set Default_State to True

        Procedure OnClick
            Set pbOK to True
            Send Close_Panel
        End_Procedure
    End_Object

    Object oCancel_btn is a Button
        Set Label to "&Cancel"
        Set Location to 58 151

        Procedure OnClick
            Set pbOK to False
            Send Close_Panel
        End_Procedure
    End_Object

//    Procedure Popup
//        Forward Send Popup
//    End_Procedure

    Procedure Page Integer iPageObject
        Forward Send Page iPageObject
        Set Icon to "DFSConfigure.ico"
    End_Procedure

    On_Key Key_Alt+Key_O Send KeyAction of oOk_btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_btn
    On_Key kCancel       Send KeyAction of oCancel_btn
End_Object

// General purpose access message for the oDFSPassword_dg dialog.
// Returns one of CI_DFSPasswordOK, CI_DFSPasswordErr or CI_DFSPasswordCancel.  
// Note: The first time the SetupDataFolder program is run with an "empty" ini-file, it will ask 
//       for a password. The password used is the default psAppDefaultPw property (= "john" 
//       until it is changed - and it should!)
Function PopupDFSPasswordDialog Integer iDataRow Returns Integer
    Handle ho hoDFS
    String sPassword sCurrentPw sDefaultAppPw
    Boolean bOK bIniFileExists bMode
    Integer iRetval

    Move "" to sPassword
    Move CI_DFSPasswordCancel to iRetval
    Get psAppDefaultPw of ghoDFSFolderSelector to sDefaultAppPw

    Move (oDFSPassword_dg(Self)) to ho
    Set psPassword of ho to ""
    Set pbOK       of ho to False

    // Popup password dialog!
    Send Popup of ho

    Get pbOK of ho to bOK

    // OK button was pressed:
    If (bOK = True) Begin
        Get psPassword of ho to sPassword
        Get ReadString of ghoDFSFolderSelector (CS_DFSIniFileDataGroup + String(iDataRow)) CS_DFSIniFilePassword "" to sCurrentPw
        If (sCurrentPw <> "") Begin
            Get DecryptPassword of ghoDFSFolderSelector sCurrentPw to sCurrentPw
        End
        Else Begin
            Move sDefaultAppPw to sCurrentPw
        End
        Move (sCurrentPw = sPassword) to bOK
        If (bOK = True) Begin
            Move CI_DFSPasswordOK to iRetval
        End
        Else Begin
            Move CI_DFSPasswordErr to iRetval
        End
    End

    Function_Return iRetval
End_Function
